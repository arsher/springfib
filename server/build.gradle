buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.4.RELEASE")
    }
}

apply plugin: 'spring-boot'

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime

    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-log4j")
    compile("javax.inject:javax.inject:1")
    compile("com.h2database:h2")
    compile("org.liquibase:liquibase-core")

    testCompile("com.google.guava:guava:16.0.1")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.skyscreamer:jsonassert:1.3.0")
    testCompile("org.dbunit:dbunit:2.5.0")
    testCompile("com.github.springtestdbunit:spring-test-dbunit:1.1.0")
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

sourceSets {
    integTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}

check.dependsOn integTest